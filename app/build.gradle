plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

apply from: "$rootDir/dependencies.gradle"

android {
    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools

    defaultConfig {
        applicationId "com.infinitumcode.tinypokedex"
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode versions.versionCode
        versionName versions.versionName
        testInstrumentationRunner "com.infinitumcode.tinypokedex.CustomTestRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation" : "$projectDir/schemas".toString()]
            }
        }
        javaCompileOptions {
            annotationProcessorOptions {
                arguments["dagger.hilt.disableModulesHaveInstallInCheck"] = "true"
            }
        }
    }

    buildFeatures {
        dataBinding true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions.freeCompilerArgs += ["-Xopt-in=kotlin.RequiresOptIn"]
    }
}

dependencies {
    // Kotlin
    implementation "androidx.core:core-ktx:1.3.2"

    // Support
    implementation "androidx.appcompat:appcompat:$versions.appCompatVersion"
    implementation "com.google.android.material:material:$versions.materialVersion"
    implementation "androidx.constraintlayout:constraintlayout:$versions.constraintVersion"
    implementation "androidx.fragment:fragment-ktx:$versions.fragmentVersion"
    implementation "androidx.drawerlayout:drawerlayout:$versions.drawerLayoutVersion"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:$versions.swipeVersion"

    // Databinding-ktx
    implementation "com.github.wada811:DataBinding-ktx:$versions.databindingKtxVersion"

    // Navigation Component
    implementation "androidx.navigation:navigation-fragment-ktx:$versions.navigationVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$versions.navigationVersion"

    // LiveData ViewModel
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$versions.lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$versions.lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$versions.lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-common-java8:$versions.lifecycleVersion"
    // Saved state module for ViewModel
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:$versions.lifecycleVersion"
    testImplementation "androidx.arch.core:core-testing:$versions.archCompomentVersion"

    // Room
    implementation "androidx.room:room-runtime:$versions.roomVersion"
    kapt "androidx.room:room-compiler:$versions.roomVersion"
    implementation "androidx.room:room-ktx:$versions.roomVersion"
    kaptAndroidTest "androidx.room:room-testing:$versions.roomVersion"

    // Paging
    implementation "androidx.paging:paging-runtime-ktx:$versions.paging3Version"

    // Di
    implementation "com.google.dagger:hilt-android:$versions.hiltCoreVersion"
    kapt "com.google.dagger:hilt-android-compiler:$versions.hiltCoreVersion"
    androidTestImplementation "com.google.dagger:hilt-android-testing:$versions.hiltCoreVersion"
    kaptAndroidTest "com.google.dagger:hilt-android-compiler:$versions.hiltCoreVersion"

    // Network
    implementation "com.squareup.retrofit2:retrofit:$versions.retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$versions.retrofitVersion"

    // Image
    implementation "io.coil-kt:coil:$versions.coilVersion"

    // Test
    testImplementation "junit:junit:$versions.junitVersion"
    androidTestImplementation "androidx.test.ext:junit:$versions.androidxTestJunit"
    androidTestImplementation "androidx.test.espresso:espresso-core:$versions.espressoVersion"
    androidTestImplementation "com.google.truth:truth:$versions.truthVersion"

    def fragment_version = "1.2.5"
    debugImplementation "androidx.fragment:fragment-testing:$fragment_version"
}